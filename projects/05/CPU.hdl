// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Mux16(a=instruction, b=outALU1, sel=instruction[15], out=a);
    // A register
    Not(in=instruction[15], out=insA);
    Or(a=insA, b=instruction[5], out=loadA);
    Register(in=a, load=loadA, out=A, out=inPC, out[0..14]=addressM);
    // Choose A or M as one ALU input
    Mux16(a=A, b=inM, sel=instruction[12], out=AorM);
    // D register
    And(a=instruction[15], b=instruction[4], out=loadD);
    DRegister(in=outALU2, load=loadD, out=D);
    // compute ALU
    ALU(x=AorM, y=D, zx=instruction[9], nx=instruction[8], zy=instruction[11], ny=instruction[10], f=instruction[7], no=instruction[6], out=outM, out=outALU1, out=outALU2, zr=zr, ng=ng);
    // writeM
    And(a=instruction[15], b=instruction[3], out=writeM);
    // JUMP
    And(a=zr, b=instruction[1], out=loadPC1);
    And(a=ng, b=instruction[2], out=loadPC2);
    Not(in=ng, out=gt1);
    Not(in=zr, out=gt2);
    And(a=gt1, b=gt2, out=gt);
    And(a=gt, b=instruction[0], out=loadPC3);
    Or(a=loadPC1, b=loadPC2, out=loadPC12);
    Or(a=loadPC12, b=loadPC3, out=loadPC123);
    And(a=instruction[15], b=loadPC123, out=loadPC);
    // PC
    PC(in=inPC, load=loadPC, inc=true, reset=reset, out[0..14]=pc);
}